plugins {
    id "com.jfrog.bintray" version "1.4"
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

def currentVersion = '0.9.7' // don't forget versionCode below!

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    resourcePrefix "binder__"

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 23
        versionCode 7
        versionName currentVersion
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    //compile 'com.android.support:appcompat-v7:23.0.1' // shall we use annotations from there?
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def bintrayUser = properties.getProperty('bintray.user')
def bintrayApiKey = properties.getProperty('bintray.key')
def gitUrl = 'https://github.com/stariy95/android-binder.git'   // Git repository URL
def siteUrl = 'https://github.com/stariy95/android-binder'      // Homepage URL of the library

version = currentVersion
group = 'com.kendamasoft'

bintray {
    user = bintrayUser
    key = bintrayApiKey
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'android-binder'
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = gitUrl
        version {
            name = currentVersion
            desc = 'Android Data Binder Beta ' + currentVersion
        }
    }
}


install {
    repositories.mavenInstaller {
        pom.project {
            name 'binder'
            description 'Simple annotation based model to view binding library'
            url siteUrl
            inceptionYear '2015'

            packaging 'aar'
            groupId 'com.kendamasoft'
            artifactId 'binder'
            version currentVersion

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection gitUrl
                url siteUrl

            }
            developers {
                developer {
                    name 'kendamasoft.com'
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    //options.encoding = 'UTF-8'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
